plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

android {
    defaultConfig {
        applicationId "com.suzukiplan.TOHOVGS"
        minSdk 26
        compileSdk 34
        targetSdk 34
        versionCode 4260
        versionName "4.26"
        ndk {
            moduleName "native-lib"
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static"
                cFlags "-fPIC " +
                        "-ffast-math " +
                        "-fsigned-char " +
                        "-Ofast " +
                        "-D__ANDROID__ " +
                        "-DFIXED_POINT " +
                        "-DNDEBUG "
                cppFlags "-std=c++1y " +
                        "-frtti " +
                        "-fexceptions " +
                        "-fPIC " +
                        "-ffast-math " +
                        "-fsigned-char " +
                        "-Ofast " +
                        "-DFORCE_DISABLE_ALIGNED_UNION " +
                        "-D__ANDROID__ " +
                        "-DFIXED_POINT " +
                        "-DNDEBUG "
            }
        }
        def properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def ADS_ID_BANNER = properties.getProperty("ads_id_banner")
        buildConfigField("String", "ADS_ID_BANNER", "\"${ADS_ID_BANNER}\"")
        def ADS_ID_REWARD = properties.getProperty("ads_id_reward")
        buildConfigField("String", "ADS_ID_REWARD", "\"${ADS_ID_REWARD}\"")
        def ADS_ID_INTERSTITIAL = properties.getProperty("ads_id_interstitial")
        buildConfigField("String", "ADS_ID_INTERSTITIAL", "\"${ADS_ID_INTERSTITIAL}\"")
        def API_SERVER_BASE_URI = properties.getProperty("api_server_base_uri")
        buildConfigField("String", "API_SERVER_BASE_URI", "\"${API_SERVER_BASE_URI}\"")
        def API_SERVER_BASE_URI_DEV = properties.getProperty("api_server_base_uri_dev")
        buildConfigField("String", "API_SERVER_BASE_URI_DEV", "\"${API_SERVER_BASE_URI_DEV}\"")
        def TEST_DEVICE_ID_1 = properties.getProperty("test_device_id_1")
        buildConfigField("String", "TEST_DEVICE_ID_1", "\"${TEST_DEVICE_ID_1}\"")
        def TEST_DEVICE_ID_2 = properties.getProperty("test_device_id_2")
        buildConfigField("String", "TEST_DEVICE_ID_2", "\"${TEST_DEVICE_ID_2}\"")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    namespace 'com.suzukiplan.tohovgs'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.google.android.gms:play-services-ads:24.4.0'
    implementation 'com.google.android.gms:play-services-appset:16.1.0'
    implementation platform('com.google.firebase:firebase-bom:33.16.0')
    implementation 'com.google.firebase:firebase-analytics-ktx:22.5.0'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.android.billingclient:billing:8.0.0'
    implementation 'com.android.billingclient:billing-ktx:8.0.0'
}

configurations.implementation {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
}
